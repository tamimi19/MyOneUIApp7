plugins {
    id 'com.android.application'
    id 'com.diffplug.spotless'
}

android {
    namespace 'com.example.oneuiapp'
    compileSdk 34

    buildFeatures {
        buildConfig true
    }

    defaultConfig {
        applicationId "com.example.oneuiapp"
        minSdk 23
        targetSdk 34
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            buildConfigField "boolean", "ENABLE_CRASH_LOGGING", "true"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

configurations.all {
    exclude group: 'androidx.appcompat', module: 'appcompat'
    exclude group: 'com.google.android.material', module: 'material'
    exclude group: 'androidx.core', module: 'core'
    exclude group: 'androidx.recyclerview', module: 'recyclerview'
    exclude group: 'androidx.coordinatorlayout', module: 'coordinatorlayout'
    exclude group: 'androidx.swiperefreshlayout', module: 'swiperefreshlayout'
    exclude group: 'androidx.preference', module: 'preference'
    exclude group: 'androidx.drawerlayout', module: 'drawerlayout'
    exclude group: 'androidx.viewpager2', module: 'viewpager2'
    exclude group: 'androidx.viewpager', module: 'viewpager'
    exclude group: 'androidx.fragment', module: 'fragment'
    exclude group: 'androidx.customview', module: 'customview'
    exclude group: 'androidx.apppickerview', module: 'apppickerview'
    exclude group: 'androidx.indexscroll', module: 'indexscroll'
    exclude group: 'androidx.picker', module: 'picker'
}

dependencies {
    // مكتبات Samsung
    implementation 'io.github.oneuiproject.sesl:appcompat:1.4.0'
    implementation 'io.github.oneuiproject.sesl:material:1.5.0'
    implementation 'io.github.oneuiproject.sesl:coordinatorlayout:1.0.0'
    implementation 'io.github.oneuiproject.sesl:drawerlayout:1.0.0'
    implementation 'io.github.oneuiproject.sesl:recyclerview:1.4.1'
    implementation 'io.github.oneuiproject.sesl:preference:1.1.0'
    implementation 'io.github.oneuiproject.sesl:swiperefreshlayout:1.0.0'
    implementation 'io.github.oneuiproject.sesl:viewpager2:1.1.0'
    implementation 'io.github.oneuiproject.sesl:viewpager:1.1.0'
    implementation 'io.github.oneuiproject.sesl:core:1.3.0'
    implementation 'io.github.oneuiproject.sesl:fragment:1.0.0'
    implementation 'io.github.oneuiproject.sesl:indexscroll:1.0.3'
    implementation 'io.github.oneuiproject.sesl:picker-basic:1.2.0'
    implementation 'io.github.oneuiproject.sesl:picker-color:1.1.0'
    implementation 'io.github.oneuiproject.sesl:apppickerview:1.0.0'
    implementation 'io.github.oneuiproject.sesl:customview:1.0.0'

    // مكتبة OneUI Design
    implementation project(path: ':oneui-lib')

    // مكتبة الايقونات
    implementation 'io.github.oneuiproject:icons:1.1.0'
    implementation 'com.airbnb.android:lottie:5.2.0'
    
    // مكتبات Google
    implementation 'androidx.annotation:annotation:1.9.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.documentfile:documentfile:1.1.0'
}

// Spotless configuration
spotless {
    java {
        target 'src/**/*.java'
        googleJavaFormat()
        removeUnusedImports()
    }
    kotlin {
        target 'src/**/*.kt'
        ktlint()
    }
    format 'xml', {
        target 'src/**/*.xml'
        eclipseWtp(com.diffplug.spotless.extra.wtp.EclipseWtpFormatterStep.XML)
    }
    format 'gradle', {
        target '**/*.gradle', '**/*.gradle.kts'
        trimTrailingWhitespace()
        leadingTabsToSpaces() // استخدمنا الخيار الصحيح هنا
        endWithNewline()
    }
}
